buildscript {
	ext {
		kotlinVersion = '1.2.31'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
	}
}

configure(subprojects.findAll { it.name != 'klient-core' }) {
	apply plugin: 'kotlin'
}

project(':klient-core') {
	apply plugin: 'kotlin-platform-common'
}

configure(subprojects.findAll { it.name != 'processor-example' }) {
	apply plugin: 'maven-publish'

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}
}

subprojects {
	group = 'com.example'
	version = '0.1.0-SNAPSHOT'
	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	kotlin {
		experimental {
			coroutines "enable"
		}
	}
}