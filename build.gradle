buildscript {
	ext {
		kotlinVersion = '1.2.40'
		ktorVersion = '0.9.2'
		serializationVersion = '0.4.2'
	}
	repositories {
		mavenCentral()
		maven { url "https://kotlin.bintray.com/kotlinx" }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
		classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$serializationVersion"
	}
}

configure(subprojects.findAll { it.name != 'krpc-core' }) {
	apply plugin: 'kotlin'
}

project(':krpc-core') {
	apply plugin: 'kotlin-platform-common'
}

configure(subprojects.findAll { it.name != 'processor-example' }) {
	apply plugin: 'maven-publish'

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}
}

subprojects {
	group = 'com.example'
	version = '0.1.0-SNAPSHOT'
	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
		maven { url "https://kotlin.bintray.com/kotlinx" }
		maven { url "https://dl.bintray.com/kotlin/ktor" }
	}

	kotlin {
		experimental {
			coroutines "enable"
		}
	}
}